---
layout: post
title: "AWK is a Turing-Complete Language"
tags: [Software, Languages, AWK]
summary: So Let's Learn It
---
{% include JB/setup %}

AWK is a Turing-complete language. So let's dive in.

The approach I'm going to take here it to introduce all the major language features via short examples. So, this isn't a complete language reference. If you want that, look [here](https://www.grymoire.com/Unix/Awk.html). We are going to try to learn AWK like a normal language. If all you really want is a handy list of those beautiful AWK one-liners, look [here](https://catonmat.net/blog/wp-content/uploads/2008/09/awk1line.txt).


## Awk vs Mawk vs Gawk

There are various flavors of `awk`.

If you're nearly any flavor of linux, `awk` almost certainly comes installed. Try typing "`man awk`" to see what flavor of awk you have. You probably have MAWK or GAWK (GNU AWK). They have a very similar API and both are fine. I *think* GAWK is somewhat more consistent across versions and platforms.


## Example 1 - Field Separators

For our first example, let's parse a little CSV file. This one shows the radiation dosage from various sources in different units (`rad.csv`):

    source,dose (Sv),dose (BED)
    eating a banana,1.0E-7,1
    arm x-ray,1.0e-6,10
    dental x-ray,5.0e-6,50
    one day natural background,1.0e-5,100
    flight from LA to New york,4.e-5,400
    mammogram,4e-4,4000
    head CT scan,2.0e-3,20000
    chest CT scan,7.0e-3,70000
    max yearly radiation worker dose,5.0e-2,5e5
    lowest yearly dose linked to cancer,0.1,1e6
    usually fatal dase,4.0,4e7

The first column in this file is the source of radiation, let's grab that on the commandline:

    $ awk -F, '{print $1}' rad.csv
    source
    eating a banana
    arm x-ray
    dental x-ray
    one day natural background
    flight from LA to New york
    mammogram
    head CT scan
    chest CT scan
    max yearly radiation worker dose
    lowest yearly dose linked to cancer
    usually fatal dase

The second column in the file is the radiation from this source in the scientific unit Sieverts (Sv). Boring. The third column is the amount of radiation from each source in the far more useful Banana Equivalent Dosages (BED). How many bananas would you need to eat to get that high a dosage? Let's find out!

    $ awk -F "," '{print $1,"\t\t",$3}' rad.csv
    source 		 dose (BED)
    eating a banana 		 1
    arm x-ray 		 10
    dental x-ray 		 50
    one day natural background 		 100
    flight from LA to New york 		 400
    mammogram 		 4000
    head CT scan 		 20000
    chest CT scan 		 70000
    max yearly radiation worker dose 		 5e5
    lowest yearly dose linked to cancer 		 1e6
    usually fatal dase 		 4e7

Okay, a dental x-ray is 50 times as much radiation as eating a banana. That seems safe. A chest CT is 70,000 BEDs; probably don't do that too often.

But you would need to each 40 million bananas to get a lethal dose of radiation. Yeah, I feel pretty safe on that one.


## Example 2 - Arithmetic Expressions

Well, how does one Banana Equivalent Dose (BED) relate to one Sievert (Sv)? Let's do a little math:

    $ awk -F, '{print $3 / $2}' rad.csv 
    -nan
    10000000
    10000000
    10000000
    10000000
    10000000
    10000000
    10000000
    10000000
    10000000
    10000000
    10000000

Oh, right, that first row in the file is a header without any numbers in it. Let's skip that:

    $ awk -F, 'NR > 1 {print $3 / $2}' rad.csv 
    10000000
    10000000
    10000000
    10000000
    10000000
    10000000
    10000000
    10000000
    10000000
    10000000
    10000000

Ah, the difference is exactly 10 million. Easy.

Well, AWK can do some basic math. Cool. Let's make a little script (`f2c.awk`):

    BEGIN {
      print(f2c(32))
      print(f2c(72))
      print(f2c(100))
    }

    function f2c(f) {
            return (f - 32.0) / 1.8
    }

Which we can run from the command line super easy:

    $ awk -f f2c.awk 
    0
    22.2222
    37.7778

So, neat. You can write AWK scripts and run them using `awk -f script.awk`. We will talk more about that `BEGIN` function later. And, yeah, you can even create your own functions and do some basic math. That's starting to seem more like a real language than just a little commnanline utility.

But this rabbit hole goes a lot deeper.


## Example 3 - Associative Arrays

Dictionaries have made Python all kinds of popular. They're less convenient, but just as awesome in Java where folks call them HashMaps. In AWK people call them "Associative Arrays". Either way, it's a data structure for making collections of key-value pairs.

For instance, let's say we want to count the number of files owned by different users in a folder. Well, `ls -l` will give us a list of files with various information, like username:

    $ ls /usr/bin/ -l
    -rwxr-xr-x 1 root        root              96 Nov 12  2018 2to3-2.7
    -rwxr-xr-x 1 root        root           10104 Apr 23  2016 411toppm
    -rwxr-xr-x 1 root        root           22696 Sep 27  2018 aa-enabled
    ...

So, that third column is the username of the owner of the file.

Here is the little script we will use to count files owned by various users, using associative arrays (`count_users.awk`):

    #!/bin/awk -f
    {
        if (NF>7) {
            username[$3]++;
        }
    }
    END {
        for (i in username) {
            print i, username[i];
        }
    }

And here we run the file, piping the raw data into it on the command line:

    $ ls /usr/bin/ -l | awk -f count_users.awk 
    thedoctor 2
    noether 4
    root 1906


## Example 4 - printf

Here's the thing. That last little program worked, but the output was nearly unreadable. Enter `printf`. If you've worked in the C language before, this is approximately what you remember.

Let's re-write our little user-counter using `printf` (`count_users2.awk`):

    #!/bin/awk -f
    BEGIN {
        # print the header
        printf("%-12s %-12s\n", "USER", "FILES OWNED");
    }
    {
        # parse the ls output
        if (NF>7) {
            username[$3]++;
        }
    }
    END {
        # print the results
        for (i in username) {
            printf("%-12s %-4d\n", i, username[i]);
        }
    }

And run this from the commandline:

    $ ls -l /usr/bin/ | awk -f count_users4.awk 
    USER      FILES OWNED 
    thedoctor 2
    noether   4
    root      1906

So, that was a lot easier to read.  Notice, we used `%-12s` to left-justify a string, rather than just `%12s`. We also used `%d` to printf an integer. And we are finally starting to see what `BEGIN` and `END` can be useful for.


## Example 5 - Random Numbers

We are going to write a program that randomly selects a line from `rad.csv` above and prints it out in some human-readable way. In the process of writing this we will see a few new features (`fun_fact.awk):`

    #!/usr/bin/gawk -f
    # older versions off AWK don't have rand() or srand()
    BEGIN {
        # seed our random number generator
        srand();
        # pick a random line in the file
        line=int(rand()*10);
        line+=3;  # let's skip the header and first row
        # commas separate the columns in our file
        FS=",";
    }
    {
        # find a random line in the file and print our fun fact
        if (NR==line) {
            printf("The radiation from a %s is equal to eating %d bananas.\n", $1, $3);
        }
    }

Hey, look at that, comments. I hear those are important. Also, we generated a random number between 0.0 and 1.0 using `rand()`. And we tried to give our code a better random seed using `srand()`.

Two other features we used are also useful when writing those amazing AWK one-liners on the commandline: `FS` and `NR`. By default, AWK splits lines into columns based on whitespace, but we can split on any string we like using `FS`. Similarly, we ONLY grab one line from our input file by specifying the line number or "number of row" with `NR`.

Enough talk, let's just run the damn script:

    $ awk -f fun_fact.awk rad.csv 
    The radiation from a head CT scan is equal to eating 20000 bananas.

Huh. So, if you need a head CT get one. But try not to turn it into a hobby.


## Example 6 - Numerical Functions

Yes, AWK has a bunch of built-in math libraries. I'm not going to list them all. But here's a flavor (`math.awk`):

    BEGIN {
    # define Pi
    PI=3.1415926;

    # print header
    printf("Degrees  Radians    sin    cos\n");

    # use some trig
    for (i=0; i<=360; i+=45) {
        r = i * (PI / 180);
        printf("%7d %8.5f %8.3f %6.3f\n", i, r, sin(r), cos(r))
    }
    exit;
    }

And when you run it from the commandline, you get what you'd expect:

    $ awk -f math.awk 
    Degrees  Radians    sin    cos
          0  0.00000    0.000  1.000
         45  0.78540    0.707  0.707
         90  1.57080    1.000  0.000
        135  2.35619    0.707 -0.707
        180  3.14159    0.000 -1.000
        225  3.92699   -0.707 -0.707
        270  4.71239   -1.000 -0.000
        315  5.49779   -0.707  0.707
        360  6.28319   -0.000  1.000

Moving on.


## Example 7 - String Functions

TODO


## Example 8 - User-Defined Functions

TODO
